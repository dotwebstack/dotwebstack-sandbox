graph config:TestTheatre {

  config:IP_DefaultResource a elmo:InformationProduct;
    elmo:query '''
        construct {
          <${subject}> ?p ?o.
          ?o rdfs:label ?olabel
        }
        where {
          <${subject}> ?p ?o
          OPTIONAL {
            ?o rdfs:label ?olabel
            FILTER (lang(?olabel)="${language}" or lang(?olabel)="")
          }
        }
      ''';
  .

  config:IP_DefaultResource_graph a elmo:InformationProduct;
    elmo:query '''
        construct {
          <${subject}> rdfs:label ?slabel.
          <${subject}> ?p ?o.
          ?o rdfs:label ?olabel.
          ?o elmo:style ?otype.
          ?or ?pr <${subject}>.
          ?or rdfs:label ?orlabel.
          ?or elmo:style ?ortype.
          <${subject}> elmo:style ?stype.
          owl:Class elmo:name "owl-Class".
          rdfs:Class elmo:name "rdfs-Class".
          skos:Concept elmo:name "skos-Concept".
        }
        where {
          {
            <${subject}> rdfs:label ?slabel.
          }
          UNION
          {
            <${subject}> rdf:type ?stype.
          }
          UNION
          {
            select distinct ?o ?p (str(?ol) as ?olabel) ?otype
            where {
              <${subject}> ?p ?o
              FILTER (isIRI(?o))
              OPTIONAL {?o rdf:type ?otype}
              OPTIONAL {
                ?o rdfs:label ?ol
                FILTER (lang(?ol)="${language}" or lang(?ol)="")
              }
            }
            limit 100
          }
          UNION
          {
            select distinct ?or ?pr (str(?orl) as ?orlabel) ?ortype
            where {
              ?or ?pr <${subject}>.
              OPTIONAL {?or rdf:type ?ortype}
              OPTIONAL {
                ?or rdfs:label ?orl
                FILTER (lang(?orl)="${language}" or lang(?orl)="")
              }
            }
            limit 100
          }
        }
      ''';
  .

}